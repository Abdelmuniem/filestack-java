apply plugin: 'java-library'

sourceCompatibility = 1.7
version = getVersion()

repositories {
    jcenter()
}

def getVersion() {
    return file(new File('VERSION')).text.trim()
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

dependencies {
    // Unit testing
    testImplementation 'junit:junit:4.12'

    // High-level HTTP client
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    // Low-level HTTP client, used by Retrofit
    compile 'com.squareup.okhttp3:okhttp:3.8.0'

    // JSON serialization / deserialization
    compile 'com.google.code.gson:gson:2.8.1'

    // Useful core libraries for Java
    compile 'com.google.guava:guava:22.0-android'
    
    // MIME type detection for files, needed for uploading
    compile 'org.apache.tika:tika-core:1.15'
}
