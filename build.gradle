apply plugin: 'java-library'

sourceCompatibility = 1.7
version = getVersion()

repositories {
    jcenter()
}

def getVersion() {
    return file(new File('VERSION')).text.trim()
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

dependencies {
    testImplementation 'junit:junit:4.12' // Testing

    compile 'com.squareup.okhttp3:okhttp:3.8.0' // Low-level HTTP client
    compile 'com.squareup.retrofit2:retrofit:2.3.0' // High-level HTTP client
    compile 'com.squareup.retrofit2:converter-gson:2.3.0' // GSON integration
    compile 'com.squareup.retrofit2:retrofit-mock:2.3.0' // Testing / mocking tools

    compile 'com.google.code.gson:gson:2.8.1' // JSON serialization to Java objects
    compile 'com.google.guava:guava:22.0-android' // Useful core libraries for Java, Android / Java 7 version

    compile 'org.apache.tika:tika-core:1.15' // MIME type detection, used for uploads
}
